//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.1;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "./interfaces/IBentoBoxMinimal.sol";
import "./interfaces/IMultiFeeDistribution.sol";
import "./interfaces/IWXTLock.sol";
import "abdk-libraries-solidity/ABDKMath64x64.sol";

contract NXUSDStaking is Ownable, ReentrancyGuard {
    using Address for address;

    struct UserData {
        APYData apyData;
        uint256 balance;
        uint256 storedReward;
        uint256 lastStakedTime;
    }

    struct APYData {
        uint256 WXTLocked;
        uint256 NXUSDByTier1;
        uint256 APYTier1;
    }

    struct Config {
        uint256 version;
        uint256 timestamp;
        APYData[] apyData;
        uint256 APYTier2;
    }

    struct HistoryRewards {
        uint256 rewardsTier1;
        uint256 rewardsTier2;
    }

    mapping (address => UserData) public userData;
    mapping (uint256 => Config) public config;
    mapping (address => HistoryRewards) public historyUserRewards;

    address immutable public stakingToken;
    IBentoBoxMinimal immutable public bentoBox;
    IMultiFeeDistribution immutable public multiFeeDistribution;
    IWXTLock public WXTLockContract;

    uint256 public treasuryBalance;
    uint256 public reservedTreasuryBalance;
    uint256 public configCurrentVersion;
    uint256 public totalPaidRewards;
    uint256 constant SECONDS_IN_YEAR = 31556952;
    uint256 constant ONE_HUNDRED_PERCENT = 100 * 1e16; // 100%

    event Staked(address user, uint256 amount);
    event Unstaked(address user, uint256 amount);
    event SetConfig(uint256 version, uint256 timestamp, APYData[] apyData, uint256 APYTier2);
    event ReplenishedTreasury(address user, uint256 amount);
    event WithdrawedTreasury(uint256 amount);

    constructor(
        IBentoBoxMinimal _bentoBox,
        IMultiFeeDistribution _multiFeeDistribution,
        address _stakingToken
    ) {
        require(address(_bentoBox).isContract(), "BentoBox is not a contract");
        require(address(_multiFeeDistribution).isContract(), "MultiFeeDistribution is not a contract");
        require(_stakingToken.isContract(), "StakingToken is not a contract");
        bentoBox = _bentoBox;
        multiFeeDistribution = _multiFeeDistribution;
        stakingToken = _stakingToken;
        _bentoBox.registerProtocol();
    }

    function getUserRewards(address user) public view returns (uint256 rewards, HistoryRewards memory historyRewards) {
       return _getUserRewards(user);
    }

    function getAPYDataConfig(uint256 version) public view returns (APYData[] memory) {
        return config[version].apyData;
    }

    function getWXTLockBalance(address user) public view returns (uint256) {
        return _checkWXTLockBalance(user);
    }

    function setWXTLockContract(IWXTLock newContract) public onlyOwner {
        require(address(newContract) != address(0), "Zero address");
        require(address(newContract).isContract(), "Not a contract");
        WXTLockContract = newContract;
    }

    function setConfig(
        uint256[] memory WXTLocked,
        uint256[] memory NXUSDByTier1,
        uint256[] memory APYTier1,
        uint256 APYTier2
    ) public onlyOwner {
        require(WXTLocked.length == NXUSDByTier1.length && WXTLocked.length == APYTier1.length, "Wrong array length");
        require(WXTLocked.length != 0, "Arrays are empty");

        configCurrentVersion = configCurrentVersion + 1;
        config[configCurrentVersion].version = configCurrentVersion;
        config[configCurrentVersion].timestamp = block.timestamp;

        config[configCurrentVersion].APYTier2 = APYTier2;

        for (uint256 i = 0; i < WXTLocked.length; i++) {
            APYData memory data;
            data.WXTLocked = WXTLocked[i];
            data.NXUSDByTier1 = NXUSDByTier1[i];
            data.APYTier1 = APYTier1[i];
            config[configCurrentVersion].apyData.push(data);
        }

        emit SetConfig(
            configCurrentVersion,
            config[configCurrentVersion].timestamp,
            config[configCurrentVersion].apyData,
            config[configCurrentVersion].APYTier2
        );
    }

    function replenishTreasury(uint256 amount) public {
        treasuryBalance = treasuryBalance + amount;
        bentoBox.deposit(stakingToken, msg.sender, address(this), amount, 0);

        emit ReplenishedTreasury(msg.sender, amount);
    }

    function withdrawTreasury(uint256 amount) public onlyOwner {
        require(amount <= treasuryBalance - reservedTreasuryBalance, "Amount is too big");
        treasuryBalance = treasuryBalance - amount;
        bentoBox.withdraw(stakingToken, address(this), owner(), amount, 0);

        emit WithdrawedTreasury(amount);
    }

    function stake(uint256 amount) public nonReentrant {
        if (userData[msg.sender].lastStakedTime == 0) {
            _updateUserAPYData(msg.sender);
            userData[msg.sender].lastStakedTime = block.timestamp;
        } else {
            _updateRewardsForUser(msg.sender);
        }

        bentoBox.deposit(stakingToken, msg.sender, address(this), amount, 0);
        userData[msg.sender].balance = userData[msg.sender].balance + amount;
        userData[msg.sender].storedReward = userData[msg.sender].storedReward + amount;

        emit Staked(msg.sender, amount);
    }

    function updateUserAPYData(address user) public nonReentrant {
        _updateUserAPYData(user);
    }

    function batchUpdateUsersAPYData(address[] memory users) public nonReentrant {
        require(users.length > 0, "Array is empty");

        for (uint256 i = 0; i < users.length; i++) {
            _updateUserAPYData(users[i]);
        }
    }

    function unstake(uint256 amount, bool fullExit) public nonReentrant {
        _updateRewardsForUser(msg.sender);
        uint256 maximumPossibleWithdraw = userData[msg.sender].storedReward;

        require(amount <= maximumPossibleWithdraw, "Amount is too big!");

        uint256 amountForWithdraw;
        uint256 userBalance = userData[msg.sender].balance;

        if (fullExit) {
            uint256 userRewards = maximumPossibleWithdraw - userBalance;
            require(treasuryBalance >= userRewards, "Treasury balance is too low!");

            amountForWithdraw = maximumPossibleWithdraw;

            totalPaidRewards = totalPaidRewards + userRewards;
            reservedTreasuryBalance = reservedTreasuryBalance - userRewards;
            treasuryBalance = treasuryBalance - userRewards;

            userData[msg.sender].storedReward = 0;
            userData[msg.sender].balance = 0;
            userData[msg.sender].lastStakedTime = 0;
            historyUserRewards[msg.sender].rewardsTier1 = 0;
            historyUserRewards[msg.sender].rewardsTier2 = 0;
        } else {
            amountForWithdraw = amount;

            if (amountForWithdraw <= userBalance) {
                userData[msg.sender].storedReward = maximumPossibleWithdraw - amountForWithdraw;
                userData[msg.sender].balance = userBalance - amountForWithdraw;
            } else {
                uint256 rewardsForWithdraw = amountForWithdraw - userBalance;
                require(treasuryBalance >= rewardsForWithdraw, "Treasury balance is too low!");

                totalPaidRewards = totalPaidRewards + rewardsForWithdraw;
                reservedTreasuryBalance = reservedTreasuryBalance - rewardsForWithdraw;
                treasuryBalance = treasuryBalance - rewardsForWithdraw;

                userData[msg.sender].storedReward = maximumPossibleWithdraw - amountForWithdraw;
                userData[msg.sender].balance = 0;
            }
        }

        bentoBox.withdraw(stakingToken, address(this), msg.sender, amountForWithdraw, 0);

        emit Unstaked(msg.sender, amountForWithdraw);
    }

    function _checkWXTLockBalance(address user) private view returns (uint256) {
        uint256 WXTLockBalance;
        if (address(WXTLockContract) != address(0)) {
            WXTLockBalance = WXTLockContract.lockedBalance(user);
        }
        (uint256 multiFeeWXTLock,,,) = multiFeeDistribution.lockedBalances(user);
        return multiFeeWXTLock + WXTLockBalance;
    }

    function _updateUserAPYData(address user) private {
        uint256 totalWxtLocked = _checkWXTLockBalance(user);

        APYData memory userAPYData;

        if (userData[user].apyData.WXTLocked != totalWxtLocked) {
            _updateRewardsForUser(user);

            userData[user].apyData.WXTLocked = totalWxtLocked;
            userAPYData = _getSpecificAPYDataConfig(user, configCurrentVersion);
            userData[user].apyData.NXUSDByTier1 = userAPYData.NXUSDByTier1;
            userData[user].apyData.APYTier1 = userAPYData.APYTier1;
        } else {
            userAPYData = _getSpecificAPYDataConfig(user, configCurrentVersion);
            userData[user].apyData.NXUSDByTier1 = userAPYData.NXUSDByTier1;
            userData[user].apyData.APYTier1 = userAPYData.APYTier1;
        }
    }

    function _updateRewardsForUser(address user) private {
        if (userData[user].lastStakedTime > 0) {
            reservedTreasuryBalance = reservedTreasuryBalance - (userData[user].storedReward - userData[user].balance);
            (uint256 rewards, HistoryRewards memory historyRewards) = _getUserRewards(user);
            historyUserRewards[user].rewardsTier1 = historyUserRewards[user].rewardsTier1 + historyRewards.rewardsTier1;
            historyUserRewards[user].rewardsTier2 = historyUserRewards[user].rewardsTier2 + historyRewards.rewardsTier2;
            userData[user].storedReward = rewards;
            reservedTreasuryBalance = reservedTreasuryBalance + userData[user].storedReward - userData[user].balance;

            uint256 totalWxtLocked = _checkWXTLockBalance(user);
            if (userData[user].apyData.WXTLocked != totalWxtLocked) {
                userData[user].apyData.WXTLocked = totalWxtLocked;
            }
            APYData memory userAPYData = _getSpecificAPYDataConfig(user, configCurrentVersion);
            userData[user].apyData.NXUSDByTier1 = userAPYData.NXUSDByTier1;
            userData[user].apyData.APYTier1 = userAPYData.APYTier1;

            userData[user].lastStakedTime = block.timestamp;
        }
    }

    function _calculateRewards(
        uint256 amount,
        APYData memory apyData,
        uint256 version,
        uint256 time
    ) private view returns (uint256 rewards, HistoryRewards memory historyRewards) {
        if (amount < apyData.NXUSDByTier1) {
            uint256 ratioTier1 = apyData.APYTier1 / SECONDS_IN_YEAR;
            (uint256 result, int128 ratioResult) = compound(amount, ratioTier1, time);
            if (result <= apyData.NXUSDByTier1) {
                rewards = result;
                historyRewards.rewardsTier1 = result - amount;
            } else {
                uint256 timeThreshold = logarithm(apyData.NXUSDByTier1, amount, ratioResult) + 1;
                if (time == timeThreshold) {
                    rewards = result;
                    historyRewards.rewardsTier1 = result - amount;
                } else {
                    uint256 calcTime = time - timeThreshold;
                    uint256 tier1RewardPerSec = apyData.NXUSDByTier1 * apyData.APYTier1 / ONE_HUNDRED_PERCENT /
                        SECONDS_IN_YEAR;
                    uint256 ratioTier2 = config[version].APYTier2 / SECONDS_IN_YEAR;

                    HistoryRewards memory calcHistoryRewards = _historyRewardsCalculation(
                        tier1RewardPerSec,
                        ratioTier2,
                        calcTime,
                        0
                    );

                    historyRewards.rewardsTier1 = calcHistoryRewards.rewardsTier1 + apyData.NXUSDByTier1 - amount;
                    historyRewards.rewardsTier2 = calcHistoryRewards.rewardsTier2;
                    rewards = historyRewards.rewardsTier1 + historyRewards.rewardsTier2 + amount;
                }
            }
        } else {
            uint256 tier1RewardPerSec = apyData.NXUSDByTier1 * apyData.APYTier1 / ONE_HUNDRED_PERCENT / SECONDS_IN_YEAR;
            uint256 ratioTier2 = config[version].APYTier2 / SECONDS_IN_YEAR;
            (uint256 tier2Reward,) = compound(amount - apyData.NXUSDByTier1, ratioTier2, time);

            HistoryRewards memory calcHistoryRewards = _historyRewardsCalculation(
                tier1RewardPerSec,
                ratioTier2,
                time,
                tier2Reward - (amount - apyData.NXUSDByTier1)
            );

            historyRewards.rewardsTier1 = calcHistoryRewards.rewardsTier1;
            historyRewards.rewardsTier2 = calcHistoryRewards.rewardsTier2;
            rewards = historyRewards.rewardsTier1 + historyRewards.rewardsTier2 + amount;
        }
    }

    function _historyRewardsCalculation(
        uint256 tier1RewardPerSec,
        uint256 ratioTier2,
        uint256 time,
        uint256 tier2Reward
    ) private pure returns (HistoryRewards memory calcHistoryRewards) {
        calcHistoryRewards.rewardsTier1 = tier1RewardPerSec * time;

        (uint256 tier2RewardWithAPRPerSec, int128 ratioResultWithAPRPerSec) = compound(
            tier1RewardPerSec,
            ratioTier2,
            time
        );
        uint256 ratioResultToUint = uint256(ABDKMath64x64.mulu(ratioResultWithAPRPerSec, 10**18));
        uint256 APYRewardsWithAPRByTime = (tier2RewardWithAPRPerSec * ratioResultToUint - tier1RewardPerSec *
        ratioResultToUint) / ratioTier2;

        if (tier2Reward > 0) {
            if (APYRewardsWithAPRByTime <= calcHistoryRewards.rewardsTier1) {
                calcHistoryRewards.rewardsTier2 = calcHistoryRewards.rewardsTier1 - APYRewardsWithAPRByTime + tier2Reward;
            } else {
                calcHistoryRewards.rewardsTier2 = APYRewardsWithAPRByTime - calcHistoryRewards.rewardsTier1 + tier2Reward;
            }
        } else {
            if (APYRewardsWithAPRByTime <= calcHistoryRewards.rewardsTier1) {
                calcHistoryRewards.rewardsTier2 = calcHistoryRewards.rewardsTier1 - APYRewardsWithAPRByTime;
            } else {
                calcHistoryRewards.rewardsTier2 = APYRewardsWithAPRByTime - calcHistoryRewards.rewardsTier1;
            }
        }
    }

    function _getUserRewards(address user) private view returns (uint256 amount, HistoryRewards memory calculatedHistoryRewards) {
        if (userData[user].lastStakedTime == 0) {
            return (amount, calculatedHistoryRewards);
        }

        uint256 time;
        uint256 userStoredReward = userData[user].storedReward;

        if (configCurrentVersion > 1 && userData[user].lastStakedTime < config[configCurrentVersion].timestamp) {
            uint256 calcRewards;
            bool needLastStakedTimeCalc = true;

            for (uint256 i = 2; i <= configCurrentVersion; i++) {
                if (userData[user].lastStakedTime > config[i].timestamp) {
                    continue;
                } else if (userData[user].lastStakedTime < config[i].timestamp && needLastStakedTimeCalc) {
                    needLastStakedTimeCalc = false;
                    uint256 configIndex = i - 1;
                    time = config[i].timestamp - userData[user].lastStakedTime;
                    APYData memory userDataOnIteration = _getSpecificAPYDataConfig(user, configIndex);
                    (uint256 rewards, HistoryRewards memory historyRewards) = _calculateRewards(
                        userStoredReward,
                        userDataOnIteration,
                        configIndex,
                        time
                    );
                    calcRewards = rewards;
                    calculatedHistoryRewards.rewardsTier1 = historyRewards.rewardsTier1;
                    calculatedHistoryRewards.rewardsTier2 = historyRewards.rewardsTier2;
                } else if (config[i].timestamp < block.timestamp) {
                    uint256 configIndex = i - 1;
                    time = config[i].timestamp - config[configIndex].timestamp;
                    APYData memory userDataOnIteration = _getSpecificAPYDataConfig(user, configIndex);
                    (uint256 rewards, HistoryRewards memory historyRewards) = _calculateRewards(
                        calcRewards,
                        userDataOnIteration,
                        configIndex,
                        time
                    );
                    calcRewards = rewards;
                    calculatedHistoryRewards.rewardsTier1 = calculatedHistoryRewards.rewardsTier1 + historyRewards.rewardsTier1;
                    calculatedHistoryRewards.rewardsTier2 = calculatedHistoryRewards.rewardsTier2 + historyRewards.rewardsTier2;
                }
            }

            time = block.timestamp - config[configCurrentVersion].timestamp;
            APYData memory userDataOnCurrentVersion = _getSpecificAPYDataConfig(user, configCurrentVersion);
            (uint256 totalRewards, HistoryRewards memory totalHistoryRewards) = _calculateRewards(
                calcRewards,
                userDataOnCurrentVersion,
                configCurrentVersion,
                time
            );
            amount = totalRewards;
            calculatedHistoryRewards.rewardsTier1 = calculatedHistoryRewards.rewardsTier1 + totalHistoryRewards.rewardsTier1;
            calculatedHistoryRewards.rewardsTier2 = calculatedHistoryRewards.rewardsTier2 + totalHistoryRewards.rewardsTier2;
        } else {
            time = block.timestamp - userData[user].lastStakedTime;
            (uint256 rewards, HistoryRewards memory historyRewards) = _calculateRewards(
                userStoredReward,
                userData[user].apyData,
                configCurrentVersion,
                time
            );
            amount = rewards;
            calculatedHistoryRewards.rewardsTier1 = historyRewards.rewardsTier1;
            calculatedHistoryRewards.rewardsTier2 = historyRewards.rewardsTier2;
        }
    }

    function _getSpecificAPYDataConfig(
        address user,
        uint256 index
    ) private view returns (APYData memory specificAPYDataConfig) {
        uint256 length = config[index].apyData.length - 1;

        for (uint256 k = 0; k <= length; length--) {
            APYData memory data = config[index].apyData[length];

            if (userData[user].apyData.WXTLocked >= data.WXTLocked) {
                specificAPYDataConfig.APYTier1 = data.APYTier1;
                specificAPYDataConfig.NXUSDByTier1 = data.NXUSDByTier1;
                break;
            }
        }
    }

    function compound(
        uint256 principal,
        uint256 ratio,
        uint256 n
    ) public pure returns (uint256 result, int128 ratioResult) {
        ratioResult = ABDKMath64x64.add(
            ABDKMath64x64.fromUInt(1),
            ABDKMath64x64.divu(ratio, 10**18)
        );
        result = ABDKMath64x64.mulu(ABDKMath64x64.pow(ratioResult, n), principal);
    }

    function logarithm(uint256 compoundResult, uint256 principal, int128 ratioResult) public pure returns (uint256) {
        int128 x = ABDKMath64x64.divu(compoundResult, principal);
        return uint256(ABDKMath64x64.toUInt(ABDKMath64x64.div(ABDKMath64x64.ln(x), ABDKMath64x64.ln(ratioResult))));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.1;

/// @notice Minimal interface for BentoBox token vault interactions - `token` is aliased as `address` from `IERC20` for code simplicity.
interface IBentoBoxMinimal {

    struct Rebase {
        uint128 elastic;
        uint128 base;
    }

    struct StrategyData {
        uint64 strategyStartDate;
        uint64 targetPercentage;
        uint128 balance; // the balance of the strategy that BentoBox thinks is in there
    }

    function strategyData(address token) external view returns (StrategyData memory);

    /// @notice Balance per ERC-20 token per account in shares.
    function balanceOf(address, address) external view returns (uint256);

    /// @notice Deposit an amount of `token` represented in either `amount` or `share`.
    /// @param token_ The ERC-20 token to deposit.
    /// @param from which account to pull the tokens.
    /// @param to which account to push the tokens.
    /// @param amount Token amount in native representation to deposit.
    /// @param share Token amount represented in shares to deposit. Takes precedence over `amount`.
    /// @return amountOut The amount deposited.
    /// @return shareOut The deposited amount repesented in shares.
    function deposit(
        address token_,
        address from,
        address to,
        uint256 amount,
        uint256 share
    ) external payable returns (uint256 amountOut, uint256 shareOut);

    /// @notice Withdraws an amount of `token` from a user account.
    /// @param token_ The ERC-20 token to withdraw.
    /// @param from which user to pull the tokens.
    /// @param to which user to push the tokens.
    /// @param amount of tokens. Either one of `amount` or `share` needs to be supplied.
    /// @param share Like above, but `share` takes precedence over `amount`.
    function withdraw(
        address token_,
        address from,
        address to,
        uint256 amount,
        uint256 share
    ) external returns (uint256 amountOut, uint256 shareOut);

    /// @notice Transfer shares from a user account to another one.
    /// @param token The ERC-20 token to transfer.
    /// @param from which user to pull the tokens.
    /// @param to which user to push the tokens.
    /// @param share The amount of `token` in shares.
    function transfer(
        address token,
        address from,
        address to,
        uint256 share
    ) external;

    /// @dev Helper function to represent an `amount` of `token` in shares.
    /// @param token The ERC-20 token.
    /// @param amount The `token` amount.
    /// @param roundUp If the result `share` should be rounded up.
    /// @return share The token amount represented in shares.
    function toShare(
        address token,
        uint256 amount,
        bool roundUp
    ) external view returns (uint256 share);

    /// @dev Helper function to represent shares back into the `token` amount.
    /// @param token The ERC-20 token.
    /// @param share The amount of shares.
    /// @param roundUp If the result should be rounded up.
    /// @return amount The share amount back into native representation.
    function toAmount(
        address token,
        uint256 share,
        bool roundUp
    ) external view returns (uint256 amount);

    /// @notice Registers this contract so that users can approve it for the BentoBox.
    function registerProtocol() external;

    function totals(address token) external view returns (Rebase memory);

    function harvest(
        address token,
        bool balance,
        uint256 maxChangeAmount
    ) external;

    function setMasterContractApproval(
        address user,
        address masterContract,
        bool approved,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    function whitelistMasterContract(address masterContract, bool approved) external;
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.1;

interface IMultiFeeDistribution {

    struct LockedBalance {
        uint256 amount;
        uint256 unlockTime;
    }

    function lockedBalances(
        address user
    ) view external returns (
        uint256 total,
        uint256 unlockable,
        uint256 locked,
        LockedBalance[] memory lockData
    );

    function stake(uint256 amount, bool lock) external;
    function withdrawExpiredLocks() external;
}

// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.1;

interface IWXTLock {
    function lockedBalance(address user) view external returns (uint256);
}

// SPDX-License-Identifier: BSD-4-Clause
/*
 * ABDK Math 64.64 Smart Contract Library.  Copyright © 2019 by ABDK Consulting.
 * Author: Mikhail Vladimirov <[email protected]>
 */
pragma solidity ^0.8.0;

/**
 * Smart contract library of mathematical functions operating with signed
 * 64.64-bit fixed point numbers.  Signed 64.64-bit fixed point number is
 * basically a simple fraction whose numerator is signed 128-bit integer and
 * denominator is 2^64.  As long as denominator is always the same, there is no
 * need to store it, thus in Solidity signed 64.64-bit fixed point numbers are
 * represented by int128 type holding only the numerator.
 */
library ABDKMath64x64 {
  /*
   * Minimum value signed 64.64-bit fixed point number may have. 
   */
  int128 private constant MIN_64x64 = -0x80000000000000000000000000000000;

  /*
   * Maximum value signed 64.64-bit fixed point number may have. 
   */
  int128 private constant MAX_64x64 = 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF;

  /**
   * Convert signed 256-bit integer number into signed 64.64-bit fixed point
   * number.  Revert on overflow.
   *
   * @param x signed 256-bit integer number
   * @return signed 64.64-bit fixed point number
   */
  function fromInt (int256 x) internal pure returns (int128) {
    unchecked {
      require (x >= -0x8000000000000000 && x <= 0x7FFFFFFFFFFFFFFF);
      return int128 (x << 64);
    }
  }

  /**
   * Convert signed 64.64 fixed point number into signed 64-bit integer number
   * rounding down.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64-bit integer number
   */
  function toInt (int128 x) internal pure returns (int64) {
    unchecked {
      return int64 (x >> 64);
    }
  }

  /**
   * Convert unsigned 256-bit integer number into signed 64.64-bit fixed point
   * number.  Revert on overflow.
   *
   * @param x unsigned 256-bit integer number
   * @return signed 64.64-bit fixed point number
   */
  function fromUInt (uint256 x) internal pure returns (int128) {
    unchecked {
      require (x <= 0x7FFFFFFFFFFFFFFF);
      return int128 (int256 (x << 64));
    }
  }

  /**
   * Convert signed 64.64 fixed point number into unsigned 64-bit integer
   * number rounding down.  Revert on underflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @return unsigned 64-bit integer number
   */
  function toUInt (int128 x) internal pure returns (uint64) {
    unchecked {
      require (x >= 0);
      return uint64 (uint128 (x >> 64));
    }
  }

  /**
   * Convert signed 128.128 fixed point number into signed 64.64-bit fixed point
   * number rounding down.  Revert on overflow.
   *
   * @param x signed 128.128-bin fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function from128x128 (int256 x) internal pure returns (int128) {
    unchecked {
      int256 result = x >> 64;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Convert signed 64.64 fixed point number into signed 128.128 fixed point
   * number.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 128.128 fixed point number
   */
  function to128x128 (int128 x) internal pure returns (int256) {
    unchecked {
      return int256 (x) << 64;
    }
  }

  /**
   * Calculate x + y.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function add (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      int256 result = int256(x) + y;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate x - y.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function sub (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      int256 result = int256(x) - y;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate x * y rounding down.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function mul (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      int256 result = int256(x) * y >> 64;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate x * y rounding towards zero, where x is signed 64.64 fixed point
   * number and y is signed 256-bit integer number.  Revert on overflow.
   *
   * @param x signed 64.64 fixed point number
   * @param y signed 256-bit integer number
   * @return signed 256-bit integer number
   */
  function muli (int128 x, int256 y) internal pure returns (int256) {
    unchecked {
      if (x == MIN_64x64) {
        require (y >= -0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF &&
          y <= 0x1000000000000000000000000000000000000000000000000);
        return -y << 63;
      } else {
        bool negativeResult = false;
        if (x < 0) {
          x = -x;
          negativeResult = true;
        }
        if (y < 0) {
          y = -y; // We rely on overflow behavior here
          negativeResult = !negativeResult;
        }
        uint256 absoluteResult = mulu (x, uint256 (y));
        if (negativeResult) {
          require (absoluteResult <=
            0x8000000000000000000000000000000000000000000000000000000000000000);
          return -int256 (absoluteResult); // We rely on overflow behavior here
        } else {
          require (absoluteResult <=
            0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);
          return int256 (absoluteResult);
        }
      }
    }
  }

  /**
   * Calculate x * y rounding down, where x is signed 64.64 fixed point number
   * and y is unsigned 256-bit integer number.  Revert on overflow.
   *
   * @param x signed 64.64 fixed point number
   * @param y unsigned 256-bit integer number
   * @return unsigned 256-bit integer number
   */
  function mulu (int128 x, uint256 y) internal pure returns (uint256) {
    unchecked {
      if (y == 0) return 0;

      require (x >= 0);

      uint256 lo = (uint256 (int256 (x)) * (y & 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF)) >> 64;
      uint256 hi = uint256 (int256 (x)) * (y >> 128);

      require (hi <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);
      hi <<= 64;

      require (hi <=
        0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF - lo);
      return hi + lo;
    }
  }

  /**
   * Calculate x / y rounding towards zero.  Revert on overflow or when y is
   * zero.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function div (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      require (y != 0);
      int256 result = (int256 (x) << 64) / y;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate x / y rounding towards zero, where x and y are signed 256-bit
   * integer numbers.  Revert on overflow or when y is zero.
   *
   * @param x signed 256-bit integer number
   * @param y signed 256-bit integer number
   * @return signed 64.64-bit fixed point number
   */
  function divi (int256 x, int256 y) internal pure returns (int128) {
    unchecked {
      require (y != 0);

      bool negativeResult = false;
      if (x < 0) {
        x = -x; // We rely on overflow behavior here
        negativeResult = true;
      }
      if (y < 0) {
        y = -y; // We rely on overflow behavior here
        negativeResult = !negativeResult;
      }
      uint128 absoluteResult = divuu (uint256 (x), uint256 (y));
      if (negativeResult) {
        require (absoluteResult <= 0x80000000000000000000000000000000);
        return -int128 (absoluteResult); // We rely on overflow behavior here
      } else {
        require (absoluteResult <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);
        return int128 (absoluteResult); // We rely on overflow behavior here
      }
    }
  }

  /**
   * Calculate x / y rounding towards zero, where x and y are unsigned 256-bit
   * integer numbers.  Revert on overflow or when y is zero.
   *
   * @param x unsigned 256-bit integer number
   * @param y unsigned 256-bit integer number
   * @return signed 64.64-bit fixed point number
   */
  function divu (uint256 x, uint256 y) internal pure returns (int128) {
    unchecked {
      require (y != 0);
      uint128 result = divuu (x, y);
      require (result <= uint128 (MAX_64x64));
      return int128 (result);
    }
  }

  /**
   * Calculate -x.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function neg (int128 x) internal pure returns (int128) {
    unchecked {
      require (x != MIN_64x64);
      return -x;
    }
  }

  /**
   * Calculate |x|.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function abs (int128 x) internal pure returns (int128) {
    unchecked {
      require (x != MIN_64x64);
      return x < 0 ? -x : x;
    }
  }

  /**
   * Calculate 1 / x rounding towards zero.  Revert on overflow or when x is
   * zero.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function inv (int128 x) internal pure returns (int128) {
    unchecked {
      require (x != 0);
      int256 result = int256 (0x100000000000000000000000000000000) / x;
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate arithmetics average of x and y, i.e. (x + y) / 2 rounding down.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function avg (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      return int128 ((int256 (x) + int256 (y)) >> 1);
    }
  }

  /**
   * Calculate geometric average of x and y, i.e. sqrt (x * y) rounding down.
   * Revert on overflow or in case x * y is negative.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function gavg (int128 x, int128 y) internal pure returns (int128) {
    unchecked {
      int256 m = int256 (x) * int256 (y);
      require (m >= 0);
      require (m <
          0x4000000000000000000000000000000000000000000000000000000000000000);
      return int128 (sqrtu (uint256 (m)));
    }
  }

  /**
   * Calculate x^y assuming 0^0 is 1, where x is signed 64.64 fixed point number
   * and y is unsigned 256-bit integer number.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @param y uint256 value
   * @return signed 64.64-bit fixed point number
   */
  function pow (int128 x, uint256 y) internal pure returns (int128) {
    unchecked {
      bool negative = x < 0 && y & 1 == 1;

      uint256 absX = uint128 (x < 0 ? -x : x);
      uint256 absResult;
      absResult = 0x100000000000000000000000000000000;

      if (absX <= 0x10000000000000000) {
        absX <<= 63;
        while (y != 0) {
          if (y & 0x1 != 0) {
            absResult = absResult * absX >> 127;
          }
          absX = absX * absX >> 127;

          if (y & 0x2 != 0) {
            absResult = absResult * absX >> 127;
          }
          absX = absX * absX >> 127;

          if (y & 0x4 != 0) {
            absResult = absResult * absX >> 127;
          }
          absX = absX * absX >> 127;

          if (y & 0x8 != 0) {
            absResult = absResult * absX >> 127;
          }
          absX = absX * absX >> 127;

          y >>= 4;
        }

        absResult >>= 64;
      } else {
        uint256 absXShift = 63;
        if (absX < 0x1000000000000000000000000) { absX <<= 32; absXShift -= 32; }
        if (absX < 0x10000000000000000000000000000) { absX <<= 16; absXShift -= 16; }
        if (absX < 0x1000000000000000000000000000000) { absX <<= 8; absXShift -= 8; }
        if (absX < 0x10000000000000000000000000000000) { absX <<= 4; absXShift -= 4; }
        if (absX < 0x40000000000000000000000000000000) { absX <<= 2; absXShift -= 2; }
        if (absX < 0x80000000000000000000000000000000) { absX <<= 1; absXShift -= 1; }

        uint256 resultShift = 0;
        while (y != 0) {
          require (absXShift < 64);

          if (y & 0x1 != 0) {
            absResult = absResult * absX >> 127;
            resultShift += absXShift;
            if (absResult > 0x100000000000000000000000000000000) {
              absResult >>= 1;
              resultShift += 1;
            }
          }
          absX = absX * absX >> 127;
          absXShift <<= 1;
          if (absX >= 0x100000000000000000000000000000000) {
              absX >>= 1;
              absXShift += 1;
          }

          y >>= 1;
        }

        require (resultShift < 64);
        absResult >>= 64 - resultShift;
      }
      int256 result = negative ? -int256 (absResult) : int256 (absResult);
      require (result >= MIN_64x64 && result <= MAX_64x64);
      return int128 (result);
    }
  }

  /**
   * Calculate sqrt (x) rounding down.  Revert if x < 0.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function sqrt (int128 x) internal pure returns (int128) {
    unchecked {
      require (x >= 0);
      return int128 (sqrtu (uint256 (int256 (x)) << 64));
    }
  }

  /**
   * Calculate binary logarithm of x.  Revert if x <= 0.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function log_2 (int128 x) internal pure returns (int128) {
    unchecked {
      require (x > 0);

      int256 msb = 0;
      int256 xc = x;
      if (xc >= 0x10000000000000000) { xc >>= 64; msb += 64; }
      if (xc >= 0x100000000) { xc >>= 32; msb += 32; }
      if (xc >= 0x10000) { xc >>= 16; msb += 16; }
      if (xc >= 0x100) { xc >>= 8; msb += 8; }
      if (xc >= 0x10) { xc >>= 4; msb += 4; }
      if (xc >= 0x4) { xc >>= 2; msb += 2; }
      if (xc >= 0x2) msb += 1;  // No need to shift xc anymore

      int256 result = msb - 64 << 64;
      uint256 ux = uint256 (int256 (x)) << uint256 (127 - msb);
      for (int256 bit = 0x8000000000000000; bit > 0; bit >>= 1) {
        ux *= ux;
        uint256 b = ux >> 255;
        ux >>= 127 + b;
        result += bit * int256 (b);
      }

      return int128 (result);
    }
  }

  /**
   * Calculate natural logarithm of x.  Revert if x <= 0.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function ln (int128 x) internal pure returns (int128) {
    unchecked {
      require (x > 0);

      return int128 (int256 (
          uint256 (int256 (log_2 (x))) * 0xB17217F7D1CF79ABC9E3B39803F2F6AF >> 128));
    }
  }

  /**
   * Calculate binary exponent of x.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function exp_2 (int128 x) internal pure returns (int128) {
    unchecked {
      require (x < 0x400000000000000000); // Overflow

      if (x < -0x400000000000000000) return 0; // Underflow

      uint256 result = 0x80000000000000000000000000000000;

      if (x & 0x8000000000000000 > 0)
        result = result * 0x16A09E667F3BCC908B2FB1366EA957D3E >> 128;
      if (x & 0x4000000000000000 > 0)
        result = result * 0x1306FE0A31B7152DE8D5A46305C85EDEC >> 128;
      if (x & 0x2000000000000000 > 0)
        result = result * 0x1172B83C7D517ADCDF7C8C50EB14A791F >> 128;
      if (x & 0x1000000000000000 > 0)
        result = result * 0x10B5586CF9890F6298B92B71842A98363 >> 128;
      if (x & 0x800000000000000 > 0)
        result = result * 0x1059B0D31585743AE7C548EB68CA417FD >> 128;
      if (x & 0x400000000000000 > 0)
        result = result * 0x102C9A3E778060EE6F7CACA4F7A29BDE8 >> 128;
      if (x & 0x200000000000000 > 0)
        result = result * 0x10163DA9FB33356D84A66AE336DCDFA3F >> 128;
      if (x & 0x100000000000000 > 0)
        result = result * 0x100B1AFA5ABCBED6129AB13EC11DC9543 >> 128;
      if (x & 0x80000000000000 > 0)
        result = result * 0x10058C86DA1C09EA1FF19D294CF2F679B >> 128;
      if (x & 0x40000000000000 > 0)
        result = result * 0x1002C605E2E8CEC506D21BFC89A23A00F >> 128;
      if (x & 0x20000000000000 > 0)
        result = result * 0x100162F3904051FA128BCA9C55C31E5DF >> 128;
      if (x & 0x10000000000000 > 0)
        result = result * 0x1000B175EFFDC76BA38E31671CA939725 >> 128;
      if (x & 0x8000000000000 > 0)
        result = result * 0x100058BA01FB9F96D6CACD4B180917C3D >> 128;
      if (x & 0x4000000000000 > 0)
        result = result * 0x10002C5CC37DA9491D0985C348C68E7B3 >> 128;
      if (x & 0x2000000000000 > 0)
        result = result * 0x1000162E525EE054754457D5995292026 >> 128;
      if (x & 0x1000000000000 > 0)
        result = result * 0x10000B17255775C040618BF4A4ADE83FC >> 128;
      if (x & 0x800000000000 > 0)
        result = result * 0x1000058B91B5BC9AE2EED81E9B7D4CFAB >> 128;
      if (x & 0x400000000000 > 0)
        result = result * 0x100002C5C89D5EC6CA4D7C8ACC017B7C9 >> 128;
      if (x & 0x200000000000 > 0)
        result = result * 0x10000162E43F4F831060E02D839A9D16D >> 128;
      if (x & 0x100000000000 > 0)
        result = result * 0x100000B1721BCFC99D9F890EA06911763 >> 128;
      if (x & 0x80000000000 > 0)
        result = result * 0x10000058B90CF1E6D97F9CA14DBCC1628 >> 128;
      if (x & 0x40000000000 > 0)
        result = result * 0x1000002C5C863B73F016468F6BAC5CA2B >> 128;
      if (x & 0x20000000000 > 0)
        result = result * 0x100000162E430E5A18F6119E3C02282A5 >> 128;
      if (x & 0x10000000000 > 0)
        result = result * 0x1000000B1721835514B86E6D96EFD1BFE >> 128;
      if (x & 0x8000000000 > 0)
        result = result * 0x100000058B90C0B48C6BE5DF846C5B2EF >> 128;
      if (x & 0x4000000000 > 0)
        result = result * 0x10000002C5C8601CC6B9E94213C72737A >> 128;
      if (x & 0x2000000000 > 0)
        result = result * 0x1000000162E42FFF037DF38AA2B219F06 >> 128;
      if (x & 0x1000000000 > 0)
        result = result * 0x10000000B17217FBA9C739AA5819F44F9 >> 128;
      if (x & 0x800000000 > 0)
        result = result * 0x1000000058B90BFCDEE5ACD3C1CEDC823 >> 128;
      if (x & 0x400000000 > 0)
        result = result * 0x100000002C5C85FE31F35A6A30DA1BE50 >> 128;
      if (x & 0x200000000 > 0)
        result = result * 0x10000000162E42FF0999CE3541B9FFFCF >> 128;
      if (x & 0x100000000 > 0)
        result = result * 0x100000000B17217F80F4EF5AADDA45554 >> 128;
      if (x & 0x80000000 > 0)
        result = result * 0x10000000058B90BFBF8479BD5A81B51AD >> 128;
      if (x & 0x40000000 > 0)
        result = result * 0x1000000002C5C85FDF84BD62AE30A74CC >> 128;
      if (x & 0x20000000 > 0)
        result = result * 0x100000000162E42FEFB2FED257559BDAA >> 128;
      if (x & 0x10000000 > 0)
        result = result * 0x1000000000B17217F7D5A7716BBA4A9AE >> 128;
      if (x & 0x8000000 > 0)
        result = result * 0x100000000058B90BFBE9DDBAC5E109CCE >> 128;
      if (x & 0x4000000 > 0)
        result = result * 0x10000000002C5C85FDF4B15DE6F17EB0D >> 128;
      if (x & 0x2000000 > 0)
        result = result * 0x1000000000162E42FEFA494F1478FDE05 >> 128;
      if (x & 0x1000000 > 0)
        result = result * 0x10000000000B17217F7D20CF927C8E94C >> 128;
      if (x & 0x800000 > 0)
        result = result * 0x1000000000058B90BFBE8F71CB4E4B33D >> 128;
      if (x & 0x400000 > 0)
        result = result * 0x100000000002C5C85FDF477B662B26945 >> 128;
      if (x & 0x200000 > 0)
        result = result * 0x10000000000162E42FEFA3AE53369388C >> 128;
      if (x & 0x100000 > 0)
        result = result * 0x100000000000B17217F7D1D351A389D40 >> 128;
      if (x & 0x80000 > 0)
        result = result * 0x10000000000058B90BFBE8E8B2D3D4EDE >> 128;
      if (x & 0x40000 > 0)
        result = result * 0x1000000000002C5C85FDF4741BEA6E77E >> 128;
      if (x & 0x20000 > 0)
        result = result * 0x100000000000162E42FEFA39FE95583C2 >> 128;
      if (x & 0x10000 > 0)
        result = result * 0x1000000000000B17217F7D1CFB72B45E1 >> 128;
      if (x & 0x8000 > 0)
        result = result * 0x100000000000058B90BFBE8E7CC35C3F0 >> 128;
      if (x & 0x4000 > 0)
        result = result * 0x10000000000002C5C85FDF473E242EA38 >> 128;
      if (x & 0x2000 > 0)
        result = result * 0x1000000000000162E42FEFA39F02B772C >> 128;
      if (x & 0x1000 > 0)
        result = result * 0x10000000000000B17217F7D1CF7D83C1A >> 128;
      if (x & 0x800 > 0)
        result = result * 0x1000000000000058B90BFBE8E7BDCBE2E >> 128;
      if (x & 0x400 > 0)
        result = result * 0x100000000000002C5C85FDF473DEA871F >> 128;
      if (x & 0x200 > 0)
        result = result * 0x10000000000000162E42FEFA39EF44D91 >> 128;
      if (x & 0x100 > 0)
        result = result * 0x100000000000000B17217F7D1CF79E949 >> 128;
      if (x & 0x80 > 0)
        result = result * 0x10000000000000058B90BFBE8E7BCE544 >> 128;
      if (x & 0x40 > 0)
        result = result * 0x1000000000000002C5C85FDF473DE6ECA >> 128;
      if (x & 0x20 > 0)
        result = result * 0x100000000000000162E42FEFA39EF366F >> 128;
      if (x & 0x10 > 0)
        result = result * 0x1000000000000000B17217F7D1CF79AFA >> 128;
      if (x & 0x8 > 0)
        result = result * 0x100000000000000058B90BFBE8E7BCD6D >> 128;
      if (x & 0x4 > 0)
        result = result * 0x10000000000000002C5C85FDF473DE6B2 >> 128;
      if (x & 0x2 > 0)
        result = result * 0x1000000000000000162E42FEFA39EF358 >> 128;
      if (x & 0x1 > 0)
        result = result * 0x10000000000000000B17217F7D1CF79AB >> 128;

      result >>= uint256 (int256 (63 - (x >> 64)));
      require (result <= uint256 (int256 (MAX_64x64)));

      return int128 (int256 (result));
    }
  }

  /**
   * Calculate natural exponent of x.  Revert on overflow.
   *
   * @param x signed 64.64-bit fixed point number
   * @return signed 64.64-bit fixed point number
   */
  function exp (int128 x) internal pure returns (int128) {
    unchecked {
      require (x < 0x400000000000000000); // Overflow

      if (x < -0x400000000000000000) return 0; // Underflow

      return exp_2 (
          int128 (int256 (x) * 0x171547652B82FE1777D0FFDA0D23A7D12 >> 128));
    }
  }

  /**
   * Calculate x / y rounding towards zero, where x and y are unsigned 256-bit
   * integer numbers.  Revert on overflow or when y is zero.
   *
   * @param x unsigned 256-bit integer number
   * @param y unsigned 256-bit integer number
   * @return unsigned 64.64-bit fixed point number
   */
  function divuu (uint256 x, uint256 y) private pure returns (uint128) {
    unchecked {
      require (y != 0);

      uint256 result;

      if (x <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF)
        result = (x << 64) / y;
      else {
        uint256 msb = 192;
        uint256 xc = x >> 192;
        if (xc >= 0x100000000) { xc >>= 32; msb += 32; }
        if (xc >= 0x10000) { xc >>= 16; msb += 16; }
        if (xc >= 0x100) { xc >>= 8; msb += 8; }
        if (xc >= 0x10) { xc >>= 4; msb += 4; }
        if (xc >= 0x4) { xc >>= 2; msb += 2; }
        if (xc >= 0x2) msb += 1;  // No need to shift xc anymore

        result = (x << 255 - msb) / ((y - 1 >> msb - 191) + 1);
        require (result <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);

        uint256 hi = result * (y >> 128);
        uint256 lo = result * (y & 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);

        uint256 xh = x >> 192;
        uint256 xl = x << 64;

        if (xl < lo) xh -= 1;
        xl -= lo; // We rely on overflow behavior here
        lo = hi << 128;
        if (xl < lo) xh -= 1;
        xl -= lo; // We rely on overflow behavior here

        assert (xh == hi >> 128);

        result += xl / y;
      }

      require (result <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);
      return uint128 (result);
    }
  }

  /**
   * Calculate sqrt (x) rounding down, where x is unsigned 256-bit integer
   * number.
   *
   * @param x unsigned 256-bit integer number
   * @return unsigned 128-bit integer number
   */
  function sqrtu (uint256 x) private pure returns (uint128) {
    unchecked {
      if (x == 0) return 0;
      else {
        uint256 xx = x;
        uint256 r = 1;
        if (xx >= 0x100000000000000000000000000000000) { xx >>= 128; r <<= 64; }
        if (xx >= 0x10000000000000000) { xx >>= 64; r <<= 32; }
        if (xx >= 0x100000000) { xx >>= 32; r <<= 16; }
        if (xx >= 0x10000) { xx >>= 16; r <<= 8; }
        if (xx >= 0x100) { xx >>= 8; r <<= 4; }
        if (xx >= 0x10) { xx >>= 4; r <<= 2; }
        if (xx >= 0x8) { r <<= 1; }
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1;
        r = (r + x / r) >> 1; // Seven iterations should be enough
        uint256 r1 = x / r;
        return uint128 (r < r1 ? r : r1);
      }
    }
  }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}