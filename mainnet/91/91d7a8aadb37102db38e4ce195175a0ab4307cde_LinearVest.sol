/**
 *Submitted for verification at snowtrace.io on 2022-07-15
*/

// SPDX-License-Identifier: MIT

pragma solidity 0.6.11;

/**
 * Lock tokens to vest over a period of time as well as includes
 * a token cliff unlock at vestStart
 */ 
contract LinearVest {
    using SafeMath for uint256;

    address public controller;
    address public beneficiary; // token recipient
    
    IERC20 public immutable token; // token that will vest

    uint public immutable vestStart; // time when vest starts
    uint public immutable vestLength; // time period over which tokens linearly vest

    uint public immutable totalVestAmount; // amount of tokens that vest across vestLength
    uint public immutable cliffUnlockAmount; // amount of tokens that unlocks immediately at vestStart

    uint public vestAmountClaimed; // amount of vested tokens claimed so far
    bool public cliffClaimed = false;

    event TokensClaimed(uint _amount, uint _time);


    modifier onlyController {
        require(
            msg.sender == controller,
            "Only the controller can call this function."
        );
        _;
    }

    constructor(
        IERC20 _token,
        address _controller,
        address _beneficiary,
        uint _vestStart,
        uint _vestLength,
        uint _vestAmount,
        uint _cliffUnlockAmount) public {

        token = _token;
        controller = _controller;
        beneficiary = _beneficiary;

        vestStart = _vestStart;
        vestLength = _vestLength;
        totalVestAmount = _vestAmount;
        cliffUnlockAmount = _cliffUnlockAmount;
    }


    // ========= EXTERNAL MUTABLE FUNCTIONS =========


    // update address that can claim tokens
    function updateController(address _newController) external onlyController {
        controller = _newController;
    }


    // update address that will receive claimed tokens
    function updateBeneficary(address _newBeneficiary) external onlyController {
        beneficiary = _newBeneficiary;
    }


    // claim vested tokens
    function claimVestedTokens(uint _amount) external onlyController {
        require(block.timestamp > vestStart, "Vesting has not started yet");

        uint availableToClaim = amountVested().sub(vestAmountClaimed);

        require(_amount <= availableToClaim, "Input amount not available to claim");

        vestAmountClaimed = vestAmountClaimed.add(_amount);

        require(token.transfer(beneficiary, _amount));

        emit TokensClaimed(_amount, block.timestamp);
    }


    // claim full cliff token amount
    function claimCliffTokens() public onlyController {
        require(block.timestamp >= vestStart, "Cliff tokens not unlocked yet");
        require(!cliffClaimed, "Cliff tokens already claimed");

        cliffClaimed = true;

        require(token.transfer(beneficiary, cliffUnlockAmount));

        emit TokensClaimed(cliffUnlockAmount, block.timestamp);
    }


    // ========= PUBLIC VIEW FUNCTIONS =========


    // returns amount of tokens that have vested
    function amountVested() public view returns (uint claimable) {
        if (block.timestamp < vestStart) {
            // time is prior to vestStart
            return 0;
        }
        uint end = getLastRelevantTime();
        uint vestTime = end.sub(vestStart);

        return totalVestAmount.mul(vestTime).div(vestLength);
    }


    // returns vestEnd if the current time is past the vest end
    // otherwise returns the current time
    function getLastRelevantTime() public view returns (uint) {
        uint vestEnd = vestStart.add(vestLength);

        if (block.timestamp > vestEnd) {
            return vestEnd;
        }

        return block.timestamp;
    }

}

/**
 * Based on the OpenZeppelin IER20 interface:
 * https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol
 *
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);
    function increaseAllowance(address spender, uint256 addedValue) external returns (bool);
    function decreaseAllowance(address spender, uint256 subtractedValue) external returns (bool);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function decimals() external view returns (uint8);
    
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

/**
 * Based on OpenZeppelin's SafeMath:
 * https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/math/SafeMath.sol
 *
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "add overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "sub overflow");
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     * - Subtraction cannot overflow.
     *
     * _Available since v2.4.0._
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "mul overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "div by 0");
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     *
     * _Available since v2.4.0._
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b != 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "mod by 0");
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts with custom message when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     *
     * _Available since v2.4.0._
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}