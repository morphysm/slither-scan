// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "./lib/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
// import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "./interfaces/ICopycatAdapter.sol";
import "./interfaces/ICopycatPlugin.sol";
import "./interfaces/IUniswapV2Router.sol";
import "./interfaces/IUniswapV2Factory.sol";
import "./interfaces/IWETH.sol";

import "./CopycatLeaderFactory.sol";
import "./CopycatLeaderStorage.sol";
import "./lib/CopycatEmergency.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

// CopycatLeader is the main contract for controlling Master fund
contract CopycatLeader is ERC20('', ''), CopycatEmergency, ReentrancyGuard {
  using SafeERC20 for IERC20;

  event ApproveToken(address indexed token, address indexed spender, uint256 amount);

  address private _owner;

  CopycatLeaderFactory public factory;
  CopycatLeaderStorage public S;
  IWETH public WETH;

  bool public initialized = false;
  uint256 public createdAt;

  // string public tokenName = "";
  // string public tokenSymbol = "";
  // string public description = "";
  // string public avatar = "";
  // string public ipfsHash;
  // uint256 public level = 0;

  bool public disabled;
  // ICopycatLeader public migratedTo;
  // address public migratedFrom;

  constructor() {
    _owner = msg.sender;
  }

  modifier onlyPlugin() {
    require(S.pluginsEnMap(address(this), ICopycatPlugin(msg.sender)), "I");
    _;
  }

  IERC20[] public tokens;
  mapping(address => uint256) public tokensType; // 1 = Normal, 2 = UniV2 LP

  function getTokens() public view returns(IERC20[] memory) {
    return tokens;
  }

  // uint256 public depositFeeRate = 0;
  // uint256 public withdrawFeeRate = 0;

  // netCopycatFee = copycatFee * copycatToken.copycatFeeMultiplier

  /**
   * @dev Tells the address of the owner
   * @return the address of the owner
   */
  function owner() public view returns(address) {
    return _owner;
  }

  modifier onlyOwner() {
    require(msg.sender == _owner, "OO");
    _;
  }

  /**
   * @dev Allows the current owner to transfer control of the contract to a newOwner.
   * @param newOwner The address to transfer ownership to.
   */
  event OwnershipTransferred(address previousOwner, address newOwner);
  function transferOwnership(address newOwner) public onlyOwner {
    require(newOwner != address(0));
    emit OwnershipTransferred(_owner, newOwner);
    _owner = newOwner;
  }

  /**
    * @dev Returns true if `account` is a contract.
    *
    * [IMPORTANT]
    * ====
    * It is unsafe to assume that an address for which this function returns
    * false is an externally-owned account (EOA) and not a contract.
    *
    * Among others, `isContract` will return false for the following
    * types of addresses:
    *
    *  - an externally-owned account
    *  - a contract in construction
    *  - an address where a contract will be created
    *  - an address where a contract lived, but was destroyed
    * ====
    */
  function isContract(address account) internal view returns (bool) {
    // This method relies on extcodesize, which returns 0 for contracts in
    // construction, since the code is only stored at the end of the
    // constructor execution.

    uint256 size;
    assembly {
      size := extcodesize(account)
    }
    return size > 0;
  }

  modifier onlyEOA(){
    bool isEOA = tx.origin == msg.sender && !isContract(msg.sender);
    require(isEOA || S.contractWhitelist(msg.sender), "EOA");
    _;
  }

  fallback() external payable {
    // WETH.deposit{value: msg.value}();
  }

  receive() external payable {
    // WETH.deposit{value: msg.value}();
  }

  function wrapEth(uint256 value) public onlyOwner {
    WETH.deposit{value: value}();
  }

  // event Initialize(
  //   address indexed initializer, 
  //   address indexed _leaderAddr
  // );
  function initialize(
    address _leaderAddr
  ) public {
    require(!initialized && _owner == address(0), "AI");
    
    factory = CopycatLeaderFactory(msg.sender);
    S = factory.S();
    WETH = S.WETH();
    createdAt = block.timestamp;
    _mint(msg.sender, 0.0001 ether);
    _addToken(WETH, 1);

    _owner = _leaderAddr;

    initialized = true;

    emit OwnershipTransferred(address(0), _owner);
    // emit Initialize(msg.sender, _leaderAddr);
  }

  // !!! Don't forget to reset allowance after disabled plugin !!! (Disable, Enable Plugin)
  // event ModifyPlugin(address indexed caller, uint256 indexed adpterId, bool enabled, address plugin);
  // function modifyPlugin(uint256 pluginId, bool enabled) external nonReentrant {
  //   ICopycatPlugin plugin = plugins[pluginId];

  //   require(factory.isAllowEmergency(ICopycatEmergencyAllower(msg.sender)) || plugin.balance() == 0, "F");

  //   pluginsEnMap[plugin] = enabled;

  //   emit ModifyPlugin(msg.sender, pluginId, enabled, address(plugin));
  // }

  function resetAllowance(IERC20 token, address spender) external nonReentrant onlyOwner {
    token.safeApproveNew(spender, 0);
    emit ApproveToken(address(token), spender, 0);
  }

  function pluginRequestAllowance(IERC20 token, address spender, uint256 amount) external virtual nonReentrant onlyPlugin {
    token.safeApproveNew(spender, amount);
    emit ApproveToken(address(token), spender, amount);
  }

  // Plugin must be approved by Copycat team. If plugin's code is not malcious, it is safe to allow this ability
  event PluginMintShare(address indexed plugin, address indexed to, uint256 amount);
  function pluginMintShare(address to, uint256 amount) external nonReentrant onlyPlugin {
    _mint(to, amount);
    emit PluginMintShare(msg.sender, to, amount);
  }

  event AddToken(address indexed adder, address indexed token, uint256 indexed tokenType);
  function _addToken(IERC20 _token, uint256 _type) internal virtual {
    if(tokensType[address(_token)] == 0) {
      tokens.push(_token);
      tokensType[address(_token)] = _type;
      emit AddToken(msg.sender, address(_token), _type);
    }
  }

  function addToken(IERC20 token) nonReentrant onlyOwner public virtual {
    require(S.getTradingRouteEnabled(address(token)), "NA");
    _addToken(token, 1);
  }

  function pluginAddToken(IERC20 token, uint256 _type) nonReentrant onlyPlugin public {
    _addToken(token, _type);
  }

  function getTokenBalance(IERC20 token) public virtual view returns(uint256 balance) {
    ICopycatAdapter[] memory adapters = S.getTokenAdapters(address(this), token);
    balance = token.balanceOf(address(this));
    for (uint256 i = 0; i < adapters.length; i++) {
      balance += adapters[i].balance();
    }
  }

  // function getTokenUsableBalance(IERC20 token) public virtual view returns(uint256) {
  //   return token.balanceOf(address(this));
  // }

  event RemoveToken(uint256 indexed i, address indexed token);
  function removeToken(uint256 i) public onlyOwner {
    // Removing WBNB is restricted
    require(i > 0 && getTokenBalance(tokens[i]) == 0, "NZ");
    emit RemoveToken(i, address(tokens[i]));
    tokensType[address(tokens[i])] = 0;
    tokens[i] = tokens[tokens.length - 1];
    tokens.pop();
  }

  event Deposit(address indexed from, address indexed to, uint256 percentage, uint256 totalShare);
  function depositTo(address to, uint256 percentage, IERC20 refToken, uint256 maxRefAmount) payable public virtual nonReentrant onlyEOA returns(uint256 totalShare) {
    require(!disabled, "D");

    uint256 refAmount = 0;
    uint256 bnbBefore = address(this).balance;

    ICopycatAdapter[] memory adapters = S.getAdapters(address(this));

    // Collect CPC fee
    uint256 depositCopycatFee = S.getLeaderDepositCopycatFee(address(this));
    if (depositCopycatFee > 0 && msg.sender != address(factory) && to != owner() && msg.sender != owner()) {
      S.collectLeaderFee(msg.sender, depositCopycatFee);
    }

    // Transfer tokens
    for (uint i = 0; i < tokens.length; i++) {
      IERC20 token = tokens[i];

      uint256 amount = token.balanceOf(address(this)) * percentage / 1e18;

      if (amount > 0) {
        if (i > 0 || msg.value == 0) {
          token.safeTransferFrom(msg.sender, address(this), amount);
        } else {
          WETH.deposit{value: amount}();
        }

        if (token == refToken) {
          refAmount += amount;
        }
      }
    }

    // Transfer to adapters
    for (uint i = 0; i < adapters.length; i++) {
      ICopycatAdapter adapter = adapters[i];
      if (S.pluginsEnMap(address(this), adapter)) {
        IERC20 token = S.adaptersToken(address(this), adapter);
        uint256 amount = adapter.balance() * percentage / 1e18;
        if (amount > 0) {
          if (token != WETH || msg.value == 0) {
            token.safeTransferFrom(msg.sender, address(adapter), amount);
          } else {
            WETH.deposit{value: amount}();
            token.safeTransfer(address(adapter), amount);
          }
          
          adapter.sync();

          if (token == refToken) {
            refAmount += amount;
          }
        }
      }
    }

    require(refAmount <= maxRefAmount, "I");

    // Mint share
    totalShare = percentage * totalSupply() / 1e18;
    uint256 shareFee = totalShare * S.getLeaderDepositPercentageFee(address(this)) / 1e18;

    if (msg.sender == address(factory) || msg.sender == owner() || to == owner()) {
      shareFee = 0;
    }

    // Reduce gas for minting
    if (shareFee > 0) {
      totalShare -= shareFee;

      _mint(owner(), shareFee * 6 / 10);
      _mint(S.feeAddress(), shareFee * 4 / 10);
    }

    _mint(to, totalShare);

    emit Deposit(msg.sender, to, percentage, totalShare);
    S.emitDeposit(msg.sender, totalShare);

    if (msg.value > 0) {
      payable(msg.sender).transfer(msg.value - (bnbBefore - address(this).balance));
    }
  }

  event Withdraw(address indexed from, address indexed to, uint256 percentage, uint256 totalShare);
  function withdrawTo(address to, uint256 shareAmount, IERC20 refToken, uint256 minRefAmount, bool asWeth) public virtual nonReentrant onlyEOA returns(uint256 percentage) {
    uint256 refAmount = 0;
    percentage = shareAmount * 1e18 / totalSupply();

    _burn(msg.sender, shareAmount);

    ICopycatAdapter[] memory adapters = S.getAdapters(address(this));

    // Transfer tokens
    for (uint i = 0; i < tokens.length; i++) {
      IERC20 token = tokens[i];

      uint256 amount = token.balanceOf(address(this)) * percentage / 1e18;

      if (amount > 0) {
        if (i > 0 || asWeth) {
          token.safeTransfer(to, amount);
        } else {
          WETH.withdraw(amount);
          payable(to).transfer(amount);
        }

        if (token == refToken) {
          refAmount += amount;
        }
      }
    }

    // Transfer to adapters
    for (uint i = 0; i < adapters.length; i++) {
      ICopycatAdapter adapter = adapters[i];
      if (S.pluginsEnMap(address(this), adapter)) {
        IERC20 token = S.adaptersToken(address(this), adapter);
        uint256 amount = adapter.balance() * percentage / 1e18;
        if (amount > 0) {
          if (token != WETH || asWeth) {
            adapter.withdrawTo(msg.sender, amount);
          } else {
            adapter.withdrawTo(address(this), amount);
            WETH.withdraw(amount);
            payable(to).transfer(amount);
          }

          if (token == refToken) {
            refAmount += amount;
          }
        }
      }
    }

    require(refAmount >= minRefAmount, "I");

    emit Withdraw(msg.sender, to, percentage, shareAmount);
    S.emitWithdraw(msg.sender, shareAmount);
  }


  function addLiquidity(
    IUniswapV2Router02 router,
    address tokenA,
    address tokenB,
    uint256 amountADesired,
    uint256 amountBDesired,
    uint256 amountAMin,
    uint256 amountBMin
  )
    external
    nonReentrant
    onlyOwner
    returns (
      uint256 amountA,
      uint256 amountB,
      uint256 liquidity
    )
  {
    require(tokensType[tokenA] != 0 && tokensType[tokenB] != 0, "E");
    require(amountADesired >= amountAMin && amountBDesired >= amountBMin, "A");

    IERC20(tokenA).safeApproveNew(address(router), amountADesired);
    IERC20(tokenB).safeApproveNew(address(router), amountBDesired);

    (amountA, amountB, liquidity) = router.addLiquidity(
      tokenA,
      tokenB,
      amountADesired,
      amountBDesired,
      amountAMin,
      amountBMin,
      address(this),
      block.timestamp
    );

    address pair = IUniswapV2Factory(router.factory()).getPair(tokenA, tokenB);
    require(S.tokenAllowed(pair), "T");
    _addToken(IERC20(pair), 2);
  }

  function removeLiquidity(
    IUniswapV2Router02 router,
    address tokenA,
    address tokenB,
    uint256 liquidity,
    uint256 amountAMin,
    uint256 amountBMin
  ) external nonReentrant onlyOwner returns (uint256 amountA, uint256 amountB) {
    require(tokensType[tokenA] != 0 && tokensType[tokenB] != 0, "E");

    IERC20(IUniswapV2Factory(router.factory()).getPair(tokenA, tokenB)).safeApproveNew(address(router), liquidity);

    (amountA, amountB) = router.removeLiquidity(
      tokenA,
      tokenB,
      liquidity,
      amountAMin,
      amountBMin,
      address(this),
      block.timestamp
    );
  }

  // function swapExactTokensForTokens(
  //   IUniswapV2Router02 router,
  //   uint256 amountIn,
  //   uint256 amountOutMin,
  //   address[] calldata path,
  //   address to,
  //   uint256 deadline
  // ) external returns (uint256[] memory amounts) {
  //   require(tokensType[path[0]] != 0 && tokensType[path[path.length - 1]] != 0, "E");

  //   IERC20(path[0]).safeApproveNew(address(router), amountIn);

  //   amounts = router.swapExactTokensForTokens(
  //     amountIn,
  //     amountOutMin,
  //     path,
  //     to,
  //     deadline
  //   );
  // }

  function mainPathSwapOut(IUniswapV2Router02 router, uint256 amountIn, address tokenIn, address tokenOut) public view returns(uint256) {
    uint256 step1 = amountIn;

    if (tokenIn != address(S.WETH())) {
      uint256[] memory step1a = router.getAmountsOut(amountIn, S.getTradingRouteSell(tokenIn));
      step1 = step1a[step1a.length - 1];
    }

    if (tokenOut == address(S.WETH())) {
      return step1;
    }

    uint256[] memory step2 = router.getAmountsOut(step1, S.getTradingRouteBuy(tokenOut));

    return step2[step2.length - 1];
  }

  function mainPathSwapIn(IUniswapV2Router02 router, uint256 amountOut, address tokenIn, address tokenOut) public view returns(uint256) {
    uint256 step1 = amountOut;

    if (tokenOut != address(S.WETH())) {
      step1 = router.getAmountsIn(amountOut, S.getTradingRouteBuy(tokenOut))[0];
    }

    if (tokenIn == address(S.WETH())) {
      return step1;
    }

    return router.getAmountsIn(step1, S.getTradingRouteSell(tokenIn))[0];
  }

  function swapExactTokensForTokensSupportingFeeOnTransferTokens(
    IUniswapV2Router02 router,
    uint256 amountIn,
    uint256 amountOutMin,
    address[] calldata path
  ) external nonReentrant onlyOwner {
    address tokenIn = path[0];
    address tokenOut = path[path.length - 1];

    require(tokensType[tokenIn] != 0 && tokensType[tokenOut] != 0, "E");

    uint256 fee = S.FEE_LIST(5) * amountIn / 1e18;
    IERC20(tokenIn).safeTransfer(S.feeAddress(), fee);
    amountIn -= fee;

    uint256 mainOut = mainPathSwapOut(router, amountIn, tokenIn, tokenOut);    
    if (mainOut > amountOutMin) amountOutMin = mainOut;

    IERC20(tokenIn).safeApproveNew(address(router), amountIn);

    router.swapExactTokensForTokensSupportingFeeOnTransferTokens(
      amountIn,
      amountOutMin,
      path,
      address(this),
      block.timestamp
    );
  }

  function swapTokensForExactTokens(
    IUniswapV2Router02 router,
    uint256 amountOut,
    uint256 amountInMax,
    address[] calldata path
  ) external nonReentrant onlyOwner returns (uint256[] memory amounts) {
    address tokenIn = path[0];
    address tokenOut = path[path.length - 1];

    require(tokensType[tokenIn] != 0 && tokensType[tokenOut] != 0, "E");

    uint256 mainIn = mainPathSwapIn(router, amountOut, tokenIn, tokenOut) * 103 / 100;
    if (mainIn < amountInMax) amountInMax = mainIn;
    
    IERC20(tokenIn).safeApproveNew(address(router), amountInMax);

    amounts = router.swapTokensForExactTokens(
      amountOut,
      amountInMax,
      path,
      address(this),
      block.timestamp
    );

    uint256 fee = S.FEE_LIST(5) * amountOut / 1e18;
    IERC20(tokenOut).safeTransfer(S.feeAddress(), fee);
  }

  event ToAdapter(address indexed caller, address indexed adapter, uint256 amount);
  function toAdapter(ICopycatAdapter adapter, uint256 amount) public nonReentrant onlyOwner {
    require(S.pluginsEnMap(address(this), adapter), "F");
    S.adaptersToken(address(this), adapter).safeTransfer(address(adapter), amount);
    adapter.sync();
    emit ToAdapter(msg.sender, address(adapter), amount);
  }

  event ToLeader(address indexed caller, address indexed adapter, uint256 amount);
  function toLeader(ICopycatAdapter adapter, uint256 amount) public nonReentrant onlyOwner {
    require(S.pluginsEnMap(address(this), adapter), "F");
    adapter.withdrawTo(address(this), amount);
    emit ToLeader(msg.sender, address(adapter), amount);
  }


  /**
    * @dev Returns the name of the token.
    */
  function name() public override view virtual returns (string memory) {
    return S.getLeaderName(address(this));
  }

  /**
    * @dev Returns the symbol of the token, usually a shorter version of the
    * name.
    */
  function symbol() public override view virtual returns (string memory) {
    return S.getLeaderSymbol(address(this));
  }

  function allowEmergencyCall(ICopycatEmergencyAllower allower, bytes32 txHash) public override view returns(bool) {
    return msg.sender == owner() && factory.isAllowEmergency(allower) && allower.isAllowed(txHash);
  }

  // event Disable(address indexed disabler, bool disabled);
  function disable(bool _disabled) public onlyOwner {
    disabled = _disabled;
    // emit Disable(msg.sender, _disabled);
  }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin guidelines: functions revert instead
 * of returning `false` on failure. This behavior is nonetheless conventional
 * and does not conflict with the expectations of ERC20 applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping (address => uint256) private _balances;

    mapping (address => mapping (address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The defaut value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor (string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5,05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `recipient` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * Requirements:
     *
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for ``sender``'s tokens of at least
     * `amount`.
     */
    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "ERC20: transfer amount exceeds allowance");
        _approve(sender, _msgSender(), currentAllowance - amount);

        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        _approve(_msgSender(), spender, currentAllowance - subtractedValue);

        return true;
    }

    /**
     * @dev Moves tokens `amount` from `sender` to `recipient`.
     *
     * This is internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `sender` cannot be the zero address.
     * - `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     */
    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        uint256 senderBalance = _balances[sender];
        require(senderBalance >= amount, "ERC20: transfer amount exceeds balance");
        _balances[sender] = senderBalance - amount;
        _balances[recipient] += amount;

        emit Transfer(sender, recipient, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        _balances[account] = accountBalance - amount;
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be to transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual { }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.0 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    // Better version of approve to prevent revert
    function safeApproveNew(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        try token.approve(spender, value) returns (bool) {} catch {
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, 0));
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
        }
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "./ICopycatPlugin.sol";

interface ICopycatAdapter is ICopycatPlugin {
  function sync() external;
  function withdrawTo(address to, uint256 amount) external;
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "../CopycatLeader.sol";

interface ICopycatPlugin {
  function pluginType() external view returns(string memory);

  function balance() external view returns(uint256);

  function contractSignature() external view returns(bytes32);

  function factory() external view returns(address);
  function leaderContract() external view returns(CopycatLeader);
  function leaderAddress() external view returns(address);
}

pragma solidity >=0.6.2;

interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}

interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}

pragma solidity >=0.5.0;

interface IUniswapV2Factory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}

pragma solidity >=0.5.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

interface IWETH is IERC20 {
    function deposit() external payable;
    function transfer(address to, uint value) external returns (bool);
    function withdraw(uint) external;
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "./CopycatLeader.sol";
import "./CopycatLeaderStorage.sol";
import "./lib/CopycatEmergencyMaster.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/proxy/Clones.sol";

interface CopygameLeaderInitialize {
  function initializeGame(
    address _leaderAddr, 
    address _staker, 
    IERC20 _baseToken, 
    uint256 _depositLimit
  ) external;
}

interface CopygameStakerInitialize {
  function initialize(address payable _shareToken) external;
}

contract CopycatLeaderFactory is CopycatEmergencyMaster, ReentrancyGuard {
  event NewCopycatLeaderContract(address indexed copycatLeader, address indexed leader, uint256 indexed leaderId);

  CopycatLeaderStorage public immutable S;
  IERC20 public immutable WETH;
  address public immutable copycatLeader;
  address public immutable copygameLeader;
  address public immutable copygameLeaderMirror;
  address public immutable copygameStaker;
  address public immutable dummyLeaderToken;

  constructor(CopycatLeaderStorage _S, address _copycatLeader, address _copygameLeader, address _copygameLeaderMirror, address _copygameStaker, address _dummyLeaderToken) {
    S = _S;
    WETH = S.WETH();
    copycatLeader = _copycatLeader;
    copygameLeader = _copygameLeader;
    copygameLeaderMirror = _copygameLeaderMirror;
    copygameStaker = _copygameStaker;
    dummyLeaderToken = _dummyLeaderToken;
  }

  function deployLeader(
    uint256 _depositCopycatFee,
    uint256 _depositPercentageFee,
    uint256 _level,
    string memory _tokenName,
    string memory _tokenSymbol,
    string memory _description,
    string memory _avatar
    // address payable _migrateFrom
  ) external nonReentrant payable returns(address payable copycatLeaderAddress) {
    require(_level < 3, "L");
    require(msg.value == 0.0001 ether, "IL");

    uint256 nextLeaderId = S.nextLeaderId();

    if (_level >= S.excludeFromFee(msg.sender)) {
      S.collectDeployFee(msg.sender, S.FEE_LIST(_level));
    }

    copycatLeaderAddress = payable(Clones.clone(copycatLeader));

    // if (_migrateFrom != address(0)) {
    //   CopycatLeader(_migrateFrom).migrateTo(CopycatLeader(copycatLeaderAddress));
    // }

    CopycatLeader(copycatLeaderAddress).initialize(
      msg.sender
    );

    S.initLeader(
      copycatLeaderAddress, 
      msg.sender,
      _depositCopycatFee,
      _depositPercentageFee,
      _level,
      _tokenName,
      _tokenSymbol,
      _description,
      _avatar,
      "trade"
    );

    // Burn and force initial leader share
    S.WETH().deposit{value: msg.value}();
    S.WETH().transfer(copycatLeaderAddress, 0.0001 ether);
    // S.WETH().approve(copycatLeaderAddress, msg.value);
    // CopycatLeader(copycatLeaderAddress).depositTo{value: 0}(msg.sender, msg.value * 10000, S.WETH(), msg.value);

    emit NewCopycatLeaderContract(copycatLeaderAddress, msg.sender, nextLeaderId);
  }

  function deployLeaderGame(
    uint256 _depositCopycatFee,
    uint256 _depositPercentageFee,
    uint256 _level,
    string memory _tokenName,
    string memory _tokenSymbol,
    string memory _description,
    string memory _avatar,
    IERC20 baseToken,
    uint256 depositLimit
  ) external nonReentrant payable returns(address payable copycatLeaderAddress) {
    require(_level < 3, "L");

    uint256 nextLeaderId = S.nextLeaderId();

    if (_level >= S.excludeFromFee(msg.sender) && address(baseToken) != dummyLeaderToken) {
      S.collectDeployFee(msg.sender, S.FEE_LIST(_level));
    }

    copycatLeaderAddress = payable(Clones.clone(copygameLeader));
    address copygameStakerAddress = payable(Clones.clone(copygameStaker));

    // if (_migrateFrom != address(0)) {
    //   CopycatLeader(_migrateFrom).migrateTo(CopycatLeader(copycatLeaderAddress));
    // }

    CopygameStakerInitialize(copygameStakerAddress).initialize(copycatLeaderAddress);

    CopygameLeaderInitialize(copycatLeaderAddress).initializeGame(
      msg.sender,
      copygameStakerAddress,
      baseToken,
      depositLimit
    );

    S.initLeader(
      copycatLeaderAddress, 
      msg.sender,
      _depositCopycatFee,
      _depositPercentageFee,
      _level,
      _tokenName,
      _tokenSymbol,
      _description,
      _avatar,
      "game"
    );

    // Burn and force initial leader share
    if (msg.value > 0 && baseToken == WETH) {
      S.WETH().deposit{value: msg.value}();
      S.WETH().transfer(copycatLeaderAddress, 0.0001 ether);
    } else {
      baseToken.transferFrom(msg.sender, copycatLeaderAddress, 0.0001 ether);
    }

    emit NewCopycatLeaderContract(copycatLeaderAddress, msg.sender, nextLeaderId);
  }

  function deployLeaderGameMirror(
    string memory _tokenName,
    string memory _tokenSymbol,
    string memory _description,
    string memory _avatar
  ) external nonReentrant payable returns(address payable copycatLeaderAddress) {
    uint256 nextLeaderId = S.nextLeaderId();

    copycatLeaderAddress = payable(Clones.clone(copygameLeaderMirror));
    address copygameStakerAddress = payable(Clones.clone(copygameStaker));

    // if (_migrateFrom != address(0)) {
    //   CopycatLeader(_migrateFrom).migrateTo(CopycatLeader(copycatLeaderAddress));
    // }

    CopygameStakerInitialize(copygameStakerAddress).initialize(copycatLeaderAddress);

    CopygameLeaderInitialize(copycatLeaderAddress).initializeGame(
      msg.sender,
      copygameStakerAddress,
      IERC20(dummyLeaderToken),
      0
    );

    S.initLeader(
      copycatLeaderAddress, 
      msg.sender,
      0,
      0,
      0,
      _tokenName,
      _tokenSymbol,
      _description,
      _avatar,
      "game_mirror"
    );

    // Burn and force initial leader share
    IERC20(dummyLeaderToken).transferFrom(msg.sender, copycatLeaderAddress, 0.0001 ether);

    emit NewCopycatLeaderContract(copycatLeaderAddress, msg.sender, nextLeaderId);
  }
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "./interfaces/IUniswapV2Router.sol";
import "./interfaces/IWETH.sol";
import "./CopycatLeader.sol";
import "./interfaces/ICopycatAdapter.sol";
import "./interfaces/ICopycatPlugin.sol";
import "./interfaces/ICopycatEmergencyAllower.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract CopycatLeaderStorage is Ownable, ReentrancyGuard {
  event FeeCollected(address indexed leaderContract, address indexed from, uint256 amount);
  event NewDeposit(address indexed leaderContract, uint256 indexed level, address indexed depositer, uint256 amount);
  event NewWithdraw(address indexed leaderContract, uint256 indexed level, address indexed withdrawer, uint256 amount);

  address public leaderFactoryAddress = address(0);

  struct LeaderInfo {
    uint256 leaderId;
    uint256 totalDeposit;
    uint256 totalWithdraw;
    // bool isUnsafe;

    uint256 depositCopycatFee;
    uint256 depositPercentageFee;

    string tokenName;
    string tokenSymbol;
    string description;
    string avatar;
    uint256 level;

    string leaderType;
  }

  struct TradingRoute {
    address router;
    address[] tradingRouteBuy;
    address[] tradingRouteSell;
    bool enabled;
  }

  mapping(address => TradingRoute) public tradingRoute;
  mapping(address => address) public factory2router;
  mapping(address => bool) public tokenAllowed;

  IERC20 immutable public copycatToken;
  IWETH immutable public WETH;

  mapping(address => LeaderInfo) private leaderInfo;
  mapping(uint256 => address) public leaderIdMap;
  mapping(address => bool) public registeredPluginFactory;
  mapping(address => uint256) public excludeFromFee;
  mapping(address => bool) public contractWhitelist;
  mapping(address => bool) public copygameContract;
  mapping(address => bool) public pairManager;

  uint256 public nextLeaderId = 1;

  uint256[6] public FEE_LIST = [200 ether, 2000 ether, 100000 ether, 1 ether, 0.3 ether, 0.001 ether];

  address public feeAddress;
  address public adminAddress;

  modifier onlyAdmin {
    require(msg.sender == adminAddress, "OA");
    _;
  }

  modifier onlyPairManager {
    require(pairManager[msg.sender], "OP");
    _;
  }

  mapping(address => ICopycatPlugin[]) public plugins;
  mapping(address => ICopycatAdapter[]) public adapters;
  mapping(address => mapping(ICopycatPlugin => uint256)) public pluginsIdMap;
  mapping(address => mapping(ICopycatPlugin => bool)) public pluginsEnMap;
  mapping(address => mapping(ICopycatAdapter => IERC20)) public adaptersToken;
  mapping(address => mapping(IERC20 => ICopycatAdapter[])) public tokenAdapters;

  function getLeaderInfo(address leader) external view returns(LeaderInfo memory) {
    return leaderInfo[leader];
  }

  function getPlugins(address leader) external view returns(ICopycatPlugin[] memory) {
    return plugins[leader];
  }

  function getAdapters(address leader) external view returns(ICopycatAdapter[] memory) {
    return adapters[leader];
  }

  function getAdaptersSpecificType(address leader, string memory leaderType) external view returns(ICopycatAdapter[] memory) {
    ICopycatAdapter[] memory result = adapters[leader];
    for (uint i = 0; i < result.length; i++) {
      if (keccak256(abi.encodePacked(result[i].pluginType())) != keccak256(abi.encodePacked(leaderType))) {
        result[i] = ICopycatAdapter(address(0));
      }
    }
    return result;
  }

  function getTokenAdapters(address leader, IERC20 token) external view returns(ICopycatAdapter[] memory) {
    return tokenAdapters[leader][token];
  }

  // Add existing initialized plugin to this leader
  event AddPlugin(address adder, address indexed leader, address indexed token, address indexed plugin);
  function addPlugin(CopycatLeader leader, address token, ICopycatPlugin plugin) nonReentrant public {
    if (registeredPluginFactory[address(plugin)]) {
      require(msg.sender == owner() && token == address(0), "F");
    } else {
      require(registeredPluginFactory[msg.sender], "F");
      require(plugin.leaderContract() == leader, "WC");
      require(plugin.leaderAddress() == leader.owner(), "WL");
    }

    uint256 pluginId = plugins[address(leader)].length;
    
    plugins[address(leader)].push(plugin);

    pluginsIdMap[address(leader)][plugin] = pluginId;
    pluginsEnMap[address(leader)][plugin] = true;

    if (token != address(0)) {
      ICopycatAdapter adapter = ICopycatAdapter(address(plugin));
      adapters[address(leader)].push(adapter);
      adaptersToken[address(leader)][adapter] = IERC20(token);
      tokenAdapters[address(leader)][IERC20(token)].push(adapter);
    }

    emit AddPlugin(msg.sender, address(leader), token, address(plugin));
  }

  // !!! Don't forget to reset allowance after disabled plugin !!! (Disable, Enable Plugin)
  event ModifyPlugin(address indexed caller, uint256 indexed adpterId, bool enabled, address plugin);
  function modifyPlugin(CopycatLeader leader, uint256 pluginId, bool enabled) external nonReentrant {
    ICopycatPlugin plugin = plugins[address(leader)][pluginId];

    require(leader.factory().isAllowEmergency(ICopycatEmergencyAllower(msg.sender)) || plugin.balance() == 0, "F");

    pluginsEnMap[address(leader)][plugin] = enabled;

    emit ModifyPlugin(msg.sender, pluginId, enabled, address(plugin));
  }

  constructor(
    IERC20 _copycatToken,
    IWETH _WETH
  ) {
    copycatToken = _copycatToken;
    WETH = _WETH;
    feeAddress = msg.sender;
    adminAddress = msg.sender;
    pairManager[msg.sender] = true;
  }

  event SetPair(address indexed tradingToken, address indexed router, bool enabled);
  function setPair(
    address _router,
    address _tradingToken,
    address[] memory _tradingRouteBuy,
    address[] memory _tradingRouteSell,
    bool _enabled
  ) public onlyPairManager {
    require(
      _tradingRouteBuy[0] == IUniswapV2Router02(_router).WETH() && 
      _tradingRouteBuy[_tradingRouteBuy.length - 1] == _tradingToken && 
      _tradingRouteSell[0] == _tradingToken &&
      _tradingRouteSell[_tradingRouteSell.length - 1] == IUniswapV2Router02(_router).WETH()
    , "V");

    factory2router[IUniswapV2Router02(_router).factory()] = _router;

    tradingRoute[_tradingToken] = TradingRoute({
      router: _router,
      tradingRouteBuy: _tradingRouteBuy,
      tradingRouteSell: _tradingRouteSell,
      enabled: _enabled
    });

    tokenAllowed[_tradingToken] = _enabled;

    emit SetPair(_tradingToken, _router, _enabled);
  }

  event SetTokenAllowed(address indexed caller, address indexed token, bool allowed);
  function setTokenAllowed(address token, bool allowed) external onlyPairManager {
    tokenAllowed[token] = allowed;
    emit SetTokenAllowed(msg.sender, token, allowed);
  }

  function getTradingRouteRouter(address _tradingToken) external view returns(address) {
    return tradingRoute[_tradingToken].router;
  }

  function getTradingRouteBuy(address _tradingToken) external view returns(address[] memory) {
    return tradingRoute[_tradingToken].tradingRouteBuy;
  }

  function getTradingRouteSell(address _tradingToken) external view returns(address[] memory) {
    return tradingRoute[_tradingToken].tradingRouteSell;
  }

  function getTradingRouteEnabled(address _tradingToken) external view returns(bool) {
    return tradingRoute[_tradingToken].enabled;
  }

  function setLeaderFactoryAddress(address _address) public {
    require(leaderFactoryAddress == address(0), "F");
    leaderFactoryAddress = _address;
  }

  function COPYCAT_FEE_BASE() public view returns(uint256) {
    return FEE_LIST[3];
  }

  event SetFee(address indexed setter, uint256 index, uint256 oldFee, uint256 newFee);
  function setFee(uint256 newFee, uint256 index) public onlyOwner {
    emit SetFee(msg.sender, index, FEE_LIST[index], newFee);
    FEE_LIST[index] = newFee;
  }

  /*event SetDeployFeeEnablePercentage(address indexed setter, uint256 oldFee, uint256 newFee);
  function setDeployFeeEnablePercentage(uint256 newFee) public onlyOwner {
    // emit SetDeployFeeEnablePercentage(msg.sender, DEPLOY_FEE_ENABLE_PERCENTAGE, newFee);
    DEPLOY_FEE_ENABLE_PERCENTAGE = newFee;
  }*/

  event SetFeeAddress(address indexed setter, address oldAddress, address newAddress);
  function setFeeAddress(address newAddress) public onlyOwner {
    emit SetFeeAddress(msg.sender, feeAddress, newAddress);
    feeAddress = newAddress;
  }

  event SetAdminAddress(address indexed setter, address oldAddress, address newAddress);
  function setAdminAddress(address newAddress) public onlyOwner {
    emit SetAdminAddress(msg.sender, adminAddress, newAddress);
    adminAddress = newAddress;
  }

  event SetRegisteredPluginFactory(address indexed setter, address indexed factoryAddress, bool enabled);
  function setRegisteredPluginFactory(address factoryAddress, bool enabled) public onlyOwner {
    registeredPluginFactory[factoryAddress] = enabled;
    emit SetRegisteredPluginFactory(msg.sender, factoryAddress, enabled);
  }

  event SetContractWhitelist(address indexed setter, address indexed contractAddr, bool enabled);
  function setContractWhitelist(address contractAddr, bool enabled) public {
    require(msg.sender == owner() || registeredPluginFactory[msg.sender], "F");
    contractWhitelist[contractAddr] = enabled;
    emit SetContractWhitelist(msg.sender, contractAddr, enabled);
  }

  event SetCopygameContract(address indexed setter, address indexed contractAddr, bool enabled);
  function setCopygameContract(address contractAddr, bool enabled) public {
    require(msg.sender == owner() || registeredPluginFactory[msg.sender], "F");
    copygameContract[contractAddr] = enabled;
    emit SetCopygameContract(msg.sender, contractAddr, enabled);
  }

  event SetPairManager(address indexed setter, address indexed contractAddr, bool enabled);
  function setPairManager(address contractAddr, bool enabled) public onlyAdmin {
    pairManager[contractAddr] = enabled;
    emit SetPairManager(msg.sender, contractAddr, enabled);
  }

  event SetExcludeFromFee(address indexed setter, address indexed walletAddress, uint256 level);
  function setExcludeFromFee(address walletAddress, uint256 level) public {
    require(msg.sender == adminAddress || msg.sender == leaderFactoryAddress, "F");
    excludeFromFee[walletAddress] = level;
    emit SetExcludeFromFee(msg.sender, walletAddress, level);
  }

  function emitDeposit(address depositer, uint256 amount) public {
    require(leaderInfo[msg.sender].leaderId > 0, "NR");
    leaderInfo[msg.sender].totalDeposit += amount;
    emit NewDeposit(msg.sender, leaderInfo[msg.sender].level, depositer, amount);
  }

  function emitWithdraw(address withdrawer, uint256 amount) public {
    require(leaderInfo[msg.sender].leaderId > 0, "NR");
    leaderInfo[msg.sender].totalWithdraw += amount;
    emit NewWithdraw(msg.sender, leaderInfo[msg.sender].level, withdrawer, amount);
  }

  function collectDeployFee(address from, uint256 amount) external {
    require(msg.sender == leaderFactoryAddress, "F");
    copycatToken.transferFrom(from, feeAddress, amount);
    emit FeeCollected(address(this), from, amount);
  }

  function collectLeaderFee(address from, uint256 amount) public {
    require(leaderInfo[msg.sender].leaderId > 0, "NR");
    uint256 half = amount * 6 / 10;
    copycatToken.transferFrom(from, CopycatLeader(payable(msg.sender)).owner(), half);
    copycatToken.transferFrom(from, feeAddress, amount - half);
    emit FeeCollected(address(this), from, amount);
  }

  function initLeader(
    address leaderContract, 
    address leader,

    uint256 _depositCopycatFee,
    uint256 _depositPercentageFee,
    uint256 _level,
    string memory _tokenName,
    string memory _tokenSymbol,
    string memory _description,
    string memory _avatar,
    string memory _leaderType
  ) external {
    require(msg.sender == leaderFactoryAddress, "F");
    require(_depositPercentageFee < 1 ether, "P");

    // Register leader to factory
    leaderInfo[leaderContract] = LeaderInfo({
      leaderId: nextLeaderId,
      totalDeposit: 0,
      totalWithdraw: 0,

      depositCopycatFee: _depositCopycatFee,
      depositPercentageFee: _depositPercentageFee,

      tokenName: _tokenName,
      tokenSymbol: _tokenSymbol,
      description: _description,
      avatar: _avatar,
      level: _level,

      leaderType: _leaderType
    });

    leaderIdMap[nextLeaderId] = leaderContract;
    excludeFromFee[leader] = 0;
    nextLeaderId++;
  }

  event UpdateLeaderInfo(
    address indexed updater,
    uint256 _depositCopycatFee,
    uint256 _depositPercentageFee,
    uint256 _level
  );
  function updateLeaderInfo(
    address leaderContract,
    uint256 _depositCopycatFee,
    uint256 _depositPercentageFee,
    uint256 _level,
    string memory _tokenName,
    string memory _tokenSymbol,
    string memory _description,
    string memory _avatar
  ) external {
    require(leaderInfo[leaderContract].leaderId > 0 && (msg.sender == Ownable(leaderContract).owner() || msg.sender == adminAddress), "F");
    require(_level < 3, "L");
    require(_depositPercentageFee < 1 ether, "P");

    LeaderInfo storage info = leaderInfo[leaderContract];

    if (_level == info.level) {
      collectLeaderFee(msg.sender, FEE_LIST[0]);
    } else {
      collectLeaderFee(msg.sender, FEE_LIST[_level]);
    }

    leaderInfo[leaderContract] = LeaderInfo({
      leaderId: info.leaderId,
      totalDeposit: info.totalDeposit,
      totalWithdraw: info.totalWithdraw,

      depositCopycatFee: _depositCopycatFee,
      depositPercentageFee: _depositPercentageFee,

      tokenName: _tokenName,
      tokenSymbol: _tokenSymbol,
      description: _description,
      avatar: _avatar,
      level: _level,

      leaderType: info.leaderType
    });

    emit UpdateLeaderInfo(msg.sender, _depositCopycatFee, _depositPercentageFee, _level);
  }

  event AdminSetLevel(address indexed setter, uint256 levelBefore, uint256 level);
  function adminSetLevel(address leaderContract, uint256 _level) public {
    require(leaderInfo[leaderContract].leaderId > 0 && msg.sender == adminAddress, "F");
    emit AdminSetLevel(msg.sender, leaderInfo[leaderContract].level, _level);
    leaderInfo[leaderContract].level = _level;
  }

  function getLeaderName(address leaderContract) public view returns(string memory) {
    return leaderInfo[leaderContract].tokenName;
  }

  function getLeaderSymbol(address leaderContract) public view returns(string memory) {
    return leaderInfo[leaderContract].tokenSymbol;
  }

  function getLeaderType(address leaderContract) public view returns(string memory) {
    return leaderInfo[leaderContract].leaderType;
  }

  function getLeaderId(address leaderContract) public view returns(uint256) {
    return leaderInfo[leaderContract].leaderId;
  }

  function getLeaderDepositCopycatFee(address leaderContract) public view returns(uint256) {
    return leaderInfo[leaderContract].depositCopycatFee * COPYCAT_FEE_BASE() / 1e18;
  }

  function getLeaderDepositPercentageFee(address leaderContract) public view returns(uint256) {
    return leaderInfo[leaderContract].depositPercentageFee;
  }
 
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "../interfaces/ICopycatEmergencyAllower.sol";

// Emergency protocol adapted from Timelock system
abstract contract CopycatEmergency {
    event ExecuteTransaction(bytes32 indexed txHash, address indexed target, uint value, string signature,  bytes data, uint eta);

    uint public constant GRACE_PERIOD = 14 days;

    function allowEmergencyCall(ICopycatEmergencyAllower allower, bytes32 txHash) public virtual view returns(bool);

    function executeTransaction(ICopycatEmergencyAllower allower, address target, uint value, string memory signature, bytes memory data, uint eta) public payable returns (bytes memory) {
        // require(msg.sender == admin, "Timelock::executeTransaction: Call must come from admin.");

        bytes32 txHash = keccak256(abi.encode(target, value, signature, data, eta));

        // We read from global control instead
        require(allowEmergencyCall(allower, txHash), "A");
        // require(queuedTransactions[txHash], "Timelock::executeTransaction: Transaction hasn't been queued.");
        require(block.timestamp >= eta, "B");
        require(block.timestamp <= eta + GRACE_PERIOD, "C");

        // queuedTransactions[txHash] = false;

        allower.beforeExecute(txHash);

        bytes memory callData;

        if (bytes(signature).length == 0) {
            callData = data;
        } else {
            callData = abi.encodePacked(bytes4(keccak256(bytes(signature))), data);
        }

        // solium-disable-next-line security/no-call-value
        (bool success, bytes memory returnData) = target.call{value: value}(callData);
        require(success, "D");

        allower.afterExecute(txHash);

        emit ExecuteTransaction(txHash, target, value, signature, data, eta);

        return returnData;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor () {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./extensions/IERC721Metadata.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/Strings.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including
 * the Metadata extension, but not including the Enumerable extension, which is available separately as
 * {ERC721Enumerable}.
 */
contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;
    using Strings for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping (uint256 => address) private _owners;

    // Mapping owner address to token count
    mapping (address => uint256) private _balances;

    // Mapping from token ID to approved address
    mapping (uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping (address => mapping (address => bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor (string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return interfaceId == type(IERC721).interfaceId
            || interfaceId == type(IERC721Metadata).interfaceId
            || super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0
            ? string(abi.encodePacked(baseURI, tokenId.toString()))
            : '';
    }

    /**
     * @dev Base URI for computing {tokenURI}. Empty by default, can be overriden
     * in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(_msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        require(operator != _msgSender(), "ERC721: approve to caller");

        _operatorApprovals[_msgSender()][operator] = approved;
        emit ApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(address from, address to, uint256 tokenId) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory _data) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `_data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(address from, address to, uint256 tokenId, bytes memory _data) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(address to, uint256 tokenId, bytes memory _data) internal virtual {
        _mint(to, tokenId);
        require(_checkOnERC721Received(address(0), to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(address from, address to, uint256 tokenId) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer of token that is not own");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits a {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(address from, address to, uint256 tokenId, bytes memory _data)
        private returns (bool)
    {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
                return retval == IERC721Receiver(to).onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    // solhint-disable-next-line no-inline-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal virtual { }
}

// SPDX-License-Identifier: BUSL-1.1-COPYCAT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "../interfaces/ICopycatEmergencyMaster.sol";
import "../interfaces/ICopycatEmergencyAllower.sol";

contract CopycatEmergencyMaster is Ownable, ICopycatEmergencyMaster {
  mapping(ICopycatEmergencyAllower => bool) public override isAllowEmergency;

  event AllowEmergency(address indexed caller, ICopycatEmergencyAllower indexed allower, bool indexed allowed);
  function allowEmergency(ICopycatEmergencyAllower allower, bool allowed) external override onlyOwner {
    isAllowEmergency[allower] = allowed;
    emit AllowEmergency(msg.sender, allower, allowed);
  }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev https://eips.ethereum.org/EIPS/eip-1167[EIP 1167] is a standard for
 * deploying minimal proxy contracts, also known as "clones".
 *
 * > To simply and cheaply clone contract functionality in an immutable way, this standard specifies
 * > a minimal bytecode implementation that delegates all calls to a known, fixed address.
 *
 * The library includes functions to deploy a proxy using either `create` (traditional deployment) or `create2`
 * (salted deterministic deployment). It also includes functions to predict the addresses of clones deployed using the
 * deterministic method.
 *
 * _Available since v3.4._
 */
library Clones {
    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create opcode, which should never revert.
     */
    function clone(address implementation) internal returns (address instance) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create(0, ptr, 0x37)
        }
        require(instance != address(0), "ERC1167: create failed");
    }

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create2 opcode and a `salt` to deterministically deploy
     * the clone. Using the same `implementation` and `salt` multiple time will revert, since
     * the clones cannot be deployed twice at the same address.
     */
    function cloneDeterministic(address implementation, bytes32 salt) internal returns (address instance) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create2(0, ptr, 0x37, salt)
        }
        require(instance != address(0), "ERC1167: create2 failed");
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(address implementation, bytes32 salt, address deployer) internal pure returns (address predicted) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf3ff00000000000000000000000000000000)
            mstore(add(ptr, 0x38), shl(0x60, deployer))
            mstore(add(ptr, 0x4c), salt)
            mstore(add(ptr, 0x6c), keccak256(ptr, 0x37))
            predicted := keccak256(add(ptr, 0x37), 0x55)
        }
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(address implementation, bytes32 salt) internal view returns (address predicted) {
        return predictDeterministicAddress(implementation, salt, address(this));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
      * @dev Safely transfers `tokenId` token from `from` to `to`.
      *
      * Requirements:
      *
      * - `from` cannot be the zero address.
      * - `to` cannot be the zero address.
      * - `tokenId` token must exist and be owned by `from`.
      * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
      * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
      *
      * Emits a {Transfer} event.
      */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(address operator, address from, uint256 tokenId, bytes calldata data) external returns (bytes4);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Metadata is IERC721 {

    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant alphabet = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = alphabet[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

pragma solidity >=0.6.6;

interface ICopycatEmergencyAllower {
  function isAllowed(bytes32 txHash) external view returns(bool);
  function beforeExecute(bytes32 txHash) external;
  function afterExecute(bytes32 txHash) external;
}

pragma solidity >=0.6.6;

import "../interfaces/ICopycatEmergencyAllower.sol";

interface ICopycatEmergencyMaster {
  function isAllowEmergency(ICopycatEmergencyAllower allower) external view returns(bool);
  function allowEmergency(ICopycatEmergencyAllower allower, bool allowed) external;
}